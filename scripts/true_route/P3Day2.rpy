label P3Day2:

#; today is yui date, get bunbunbun to steal kat's master key to get the files (library)
#; the files will tell how the guards will respond, and Allie also needs the info there for the escape
#; probably start this with kat, then flashback
window show dissolve
play music Morning
show bg MansionMorning with dissolve

show k flirt at pos50k with dissolve
k "Congrats, [name]! You've made it to day two!"
show k laugh
k "You really gave our audience a run for their money yesterday with Scarlett."
show k surprised
k "...And that's before we got the zombies involved, believe it or not."
m "I'm glad to hear that."
show k laugh


menu:
    k "You think you'll choose Scarlett at the end of this?"
    "Definitely":
        jump p3d2_definitely
    "Too early to say":
        jump p3d2_too_early


label p3d2_definitely:
"I figure it's best to make things seem uncertain as possible, and not get too focused on any one girl."
"Kat said that getting low ratings during the show could end up with them shutting it down early."
"If that happened this time, then... game over."
"The only reason why we're able to make these moves right now is because Allie's machine glitched out - who knows if that would ever happen again."
"...There's no room for error."
m "She's definitely my number one right now, but who knows?"
m "We'll have to wait and see, but... I've got a really good feeling about her."

jump p3d2_addAfter

label p3d2_too_early:
"I figure it's best to make things seem uncertain as possible, and not get too focused on any one girl."
"Kat said that getting low ratings during the show could end up with them shutting it down early."
"If that happened this time, then... game over."
"The only reason why we're able to make these moves right now is because Allie's machine glitched out - who knows if that would ever happen again."
"...There's no room for error."
m "Too early to say right now... but she's definitely up there, Kat."

label p3d2_addAfter:

show k flirt
k "I like the sound of that."
show k happy
k "But you know what I like even more? Drama."
show k sassy
k "And what better way to start that fire, than a full-day date with another woman?"
m "If it ain't broke..."
show k neutral
k "So who's it gonna be?"
m "..."
stop music
play music ElectroCabello

hide k with dissolve
show bg Lab with dissolve
show a neutral at pos50a with dissolve
a "Have you ever noticed that there's only one room in the mansion that's locked?"
m "Nope! Not even a bit."
show a surprised
a "...Really! It's right by the Games Room, and it's not the hidden one."
show a laugh
a "I'm starting to understand why you've been stuck here all this time."
show a neutral
a "I was snooping around at night looking for snacks, when I saw Kat open it - using a black key that she kept on a key ring in her pocket."
show a sassy
a "It's definitely a master key. I've seen her use the same key both in the mansion, as well as on other buildings."
m "...Sounds like it'd be pretty good to get our hands on that."
show a happy
a "Exactly!"
show a sassy
a "If you get me that key, for even a minute - I can make a mold of it that we can use as a duplicate key."
show a laugh
a "I'm betting that would be pretty helpful for helping us get outta here, to put it lightly."
m "That's awesome! But wouldn't Kat notice her master key going missing?"
show a sassy
a "You're absolutely right! Especially if you or I try to take it."
m "I'm feeling a \"but\" coming."
show a laugh
a "...But it won't be you or I taking it."
m "...?"
show a happy
a "Tomorrow, ask to take Yui on a date."
show a sassy
a "Let me go get Yui. By the end of tonight, you'll both understand why it has to be you two."
"I'd say \"sounds like a plan\", but I have no idea what's going on."
m "...Okay...?"
show a laugh
a "Trust me, [name]! When have I ever been wrong?"
m "I could list every single date we'd been on as evidence."
show a angry
a "Those don't count."
hide a with dissolve
show bg MansionMorning with dissolve
show k flirt at pos50k with dissolve
stop music
play music Morning
m "...Yui."
m "There's just something about her, you know?"
show k laugh
k "We'll make it happen!"
hide k with dissolve
show bg Black with dissolve
"..."
stop music
play music TouchingMoments
show bg Hills with dissolve
show y happy at pos50y with dissolve
"It was the first date since getting all of the girls' memories back."
"We went for a walk out in the woods - the same date we had when we truly had met again for the first time."
"It was a little awkward at first, figuring out how to not give away what we knew on camera, but we figured it out pretty quick."
y "It's so nice to see you again, [name]."
m "I feel the same way, Yui."
show y laugh
y "After all this time... it's got to be fate, hasn't it?"
m "...It must be."
show y worried
y "To be honest, I feel a little like things are going a little over my head."
show y sad
y "...This show, meeting you again, everything, it's... it's just moving so fast."
show y laugh
y "Sometimes I feel like everything's just moving too fast for me to keep up."
show y happy
y "...But with you... everything feels alright. Like I know it's going to be okay."
show y blush
y "...Would it be okay if we just spend today this way?"
"She leaned in closer and put her arms around me."
m "I wouldn't have it any other way, Yui."
"Wait a minute - what about getting the master key?"
show y laugh
y "Ah... I can tell from your face what you're thinking about."
show y happy
y "Don't worry, I've got that covered."
show y angry
y "Bunbunbun!"
hide y  with dissolve
show bg YuiHoldingBunny with dissolve
"Bunbunbun darted out of a nearby bush, and ran to Yui's side."
#show y happy
y "Who's a good boy! You're a good boy!"
"She picked him up and hugged him tight."
"It was impossible not to smile."
#show y laugh
y "...Almost forgot!"
#show y happy
y "Bunbunbun..."
stop music
play music RocketPower
#show y laugh
y "...HUNT."
show bg Hills with dissolve
show y happy at pos50y with dissolve
"Bunbunbun leapt out of her arms, then dashed towards the mansion."
show y happy
y "And with that out of the way... how about we walk for a little longer?"
"...What just happened?"
stop music
play music LoveTheme
"She reached for my hand, then held it tight."
m "...I'd like that."
"We walked and talked for hours."
"When we took breaks, we laid next to each other on the grass, talking about anything and everything."
show bg LakeEvening with dissolve
"Yui's a lovely girl."
"At times, I remembered how dire the situation we were in, and I couldn't help but freeze up."
"But with Yui, I'm never afraid to face those fears. She makes me braver with just a smile."
"Even when she couldn't ask directly about what was on my mind, she found ways to comfort me despite that."
"And in turn... to make her smile made me feel lighter than air."
"...A life with her would be wonderful."
"I've got to make sure to get us all out of here safe."
show y blush
y "...[name]?"
m "Yeah?"
show y happy
y "I love you, [name]."
show y laugh
y "...I hope we can spend another day together, just like this, soon."

menu:
    "I can't wait for that day":
        jump p3d2_c3
    "I'd like that":
        jump p3d2_c4

label p3d2_c3:
show y blush
m "I can't wait for that day, Yui."
show y happy
y "I'll be counting the days till then!"
jump p3d2_addAfter2

label p3d2_c4:
m "...Me too, Yui."

label p3d2_addAfter2:

show bg Black with dissolve
hide y with dissolve
"We walked back to the mansion together, hand in hand, then went our separate ways."
show bg MansionNight with dissolve
show k surprised at pos50k with dissolve
stop music
play music MoveForward
k "...[name]! You would not believe what happened to me today."
m "What happened, Kat?"
show k angry
k "I got attacked by some kind of forest monster!"
show k surprised
k "It was at least ten feet tall, had monstrous teeth, and -"
show k angry
k "There was at least five of them!"
"It took everything I had to not laugh."
show k annoyed
k "Luckily, I managed to fight them off without any permanent injury."
show k worried
k "I made sure that Four deleted the footage - that shit was both embarassing and traumatic."
"...! That's good to know."
show k sad
k "...This is the worst run yet, man."
m "What do you mean by that?"
show k surprised
k "Oh, nothing. I've just... had a long day."
m "I'll bet."
m "You should get some rest while you have the chance. Five forest beasts is nothing to sniff at!"
show k flirt
k "...You're making fun of me, aren't you?"
m "Not at all, Kat."
m "Forest beasts affect one in ten people. It could happen to you."
show k sassy
k "Right, right. I'll see you later, [name]."
k "Good night, [name]."
show bg Black with dissolve
hide k with dissolve
"..."
stop music
play music Wholesome
show bg Lab with dissolve
show y happy at pos30y with dissolve
show a neutral at pos70a with dissolve
y "...Mission success!"
show a worried
a "I can't believe that worked."
show a surprised
a "How did you train your pet bunny to do all that?"
show y laugh
y "It's all thanks to our special training last night!"
show y happy
y "I may have made a few contracts with a few devils from the forest, but..."
show a surprised
a "I'm sorry, what?"
show y happy
y "I'm half kidding, don't worry about it."
"...Which half?"
show y neutral
y "Are the keys ready to use?"
show a worried
a "Not yet, which sucks. It's going to take a little longer for the makeshift plaster to set."
m "Where'd you get the plaster from, by the way?"
show a laugh
a "Believe me, knowing more about how we got it will not make you any happier as a person."
m "I'm cool with not knowing."
show a neutral
a "...Anyway, the keys will probably be usable by tomorrow morning - but you'll have to have your next date by then."
show a happy
a "We'll test them out as soon as it's night tomorrow. Sound good?"
m "Sounds good to me."
show y laugh
y "Awesome! Let's go, team!"
show a happy
a "How about you let the rest of the girls know, [name]?"
show a neutral
a "Four's disabled the audio recordings in the house at night, so we should be good to talk back up there."
m "I'll let them know."
hide y with dissolve
hide a with dissolve
show bg Black with dissolve
"I walked back up the staircase, back to the mansion."
show bg GamesRoomNight with dissolve
stop music
play music Bittersweet
"Where are they - ah. I can hear some sounds from the kitchen."
show bg KitchenNight with dissolve
"I'll make my way over."
show v serious at pos50v with dissolve
v "..."
m "Hey Violet, how are you?"
show v neutral
v "...?"
show v surprised
v "Goodness! Apologies, [name]. I didn't see you there."
m "You seemed pretty deep in thought. What's on your mind?"
show v laugh
v "Besides the say, hundreds of different versions of memories of the same week?"
show v sad
v "...I'm worried for the people back home."
show v worried
v "Yui has her sisters to worry about, though she tries not to burden anyone with that feeling."
show v sad
v "Everyone has family out there - and who knows what's happened to them in the last... well, however long we've been trapped here."
show v worried
v "...I have never gotten along with my parents that well, but even so... they must be worried sick."
show v sad
v "..."
m "I'm sure they're doing fine, Violet."
m "And they're going to be so happy to see you again."
show v happy
v "...You're right, [name]."
show v laugh
v "Though there's no way you could know that for sure, could you."
m "I guess so, but... we've got to believe, right?"
show v happy
v "...I suppose you're right once again, [name]."
show v laugh
v "When we finally escape this dreadful place... I'm going to hold them so tight."
show v neutral
v "...It's kind of funny."
m "What is, Violet?"
show v happy
v "I told you I had a twin sister, right?"
m "Yeah, you did. Her name was... Viola?"
show v laugh
v "That would be her."
show v neutral
v "...She was always my parents' favorite."
show v laugh
v "You know the type. She came first in just about everything, while I... did not."
show v happy
v "Even so, we got along just fine. I was proud to call her my sister."
show v worried
v "...Then there was the accident. She..."
show v sad
v "...She saw the car before I did. She moved to protect me before I could. And in the end..."
show v sad
v "She was hit before I was... and she died for it."
m "I'm so sorry, Violet. I..."
show v worried
v "Before then, I... had no problem being second place to her."
show v laugh
v "...But for her sake, for just that one time... I wish I had come in first."
show v sad
v "I'm oversharing, aren't I? I apologize, I - it's been a -"
m "It's okay, Violet. Just say what you're feeling."
show v neutral
v "...You're kind, [name]. It's something I adore about you, you know?"
show v worried
v "After Viola died, I did my very best to live up to the standard she set for my parents."
show v laugh
v "You can probably guess how well that went."
show v sad
v "I wasn't as good as running the business, or appearing \"prim\" and \"proper\", or... even things like finding a partner."
show v worried
v "It's a little embarassing. Really, that's the whole reason why I came here."
show v happy
v "My parents saw an ad for the show, and thought it might help me."
show v laugh
v "Or perhaps they just wanted to kick me out of the house, to make more room for my new baby brother."
show v happy
v "I couldn't resist the chance for them to support me for once."
show v sad
v "...At times, I feel like I can't do anything right. Look at how this show has developed."
show v worried
v "This is a nightmare in every regard."
m "...Violet."
show v worried
v "...Yes?"
stop music
play music LoveTheme
"I held her in my arms tightly."
show v blush
v "...?"
m "You're an incredible woman, and believe me, there's nothing you can't do."
m "I've seen you put the fear of god into the Brothers Five, and make delicious meals that some people would probably kill for."
m "Even if this is a nightmare, it's one we're going to get out of together - and we wouldn't be able to do it without you."
show v sad
v "..."
show v laugh
v "You are too kind."
show v blush
v "T... Thank you, for the... I appreciate it."
show v happy
v "...But enough about me."
show v neutral
v "Have you remembered anything about why you came here?"
m "Unfortunately, I haven't... but I'm hoping that the master key we pilfered will help with that."
show v happy
v "I hope so, for your sake."
show v worried
v "In the event where it does not provide any answers..."
show v happy
v "...My previous offer about finding you help outside of here, to get your memories back, stands."
m "...Thanks, Violet. I really appreciate that."
show v blush
v "It is the least I could do."
show v worried
v "I still... fancy you, quite a bit, you know?"
show v happy
v "I know it's not the time or place, but maybe when we get out of here..."
show v laugh
v "Perhaps you'll join me for... say, a lovely cruise around what's left of the world?"
m "...I'm looking forward to that day."
show v happy
v "As am I."
"We talked for a few moments longer, before retiring for the night."
hide v with dissolve
show bg RoomNight with dissolve
"For once, I lay in bed not worrying about being killed."
"For once, I was excited for what tomorrow would bring."
"It's only up from here."
"I-"
stop music
play music MasterDisorder
show a surprised at pos50a
a "[name]!"
m "Allie? What are you -"
show a worried
a "Never mind that! You've got to come down to the lab."
m "What is it?"
show a serious
a "...We found something, and I know you'll want to see it."
"She reached for my hand and pulled me as we ran."
show bg Black with dissolve
"..."
show bg Lab with dissolve
show y worried at pos10y with dissolve # former look:right
show s worried at pos30s with dissolve
show a serious  at pos50a
show v worried at pos70v with dissolve # former ,,-1
show t worried at pos90t with dissolve
"The other girls were already there, crowded around a table with a pile of folders on top of it."
m "What's going on?"
show y worried
y "I was just looking around the lab, when I stumbled across these."
show y serious
y "...They're our files."
show y worried
y "I didn't read anyone's but my own, but I have to warn you..."
"Caution was the last thing on all of our minds."
"Most of us scrambled to find our own file - except for Yui, who was already holding hers, and Allie."
"...Weird, there's no file for me here."
"I double-checked, but there was nothing for me."
"Guess I'm not getting any answers today."
show a neutral
"Allie slowly walked to the pile, found her own, then opened it in one fluid motion."
show a surprised
a "...Interesting!"
m "What is it?"
show a neutral
a "\"Allie Major. Sent to the Island for being suspected of committing treason against the government.\""
a "\"...Selected for the experimental show for fitting the 'sporty girl' archetype - we need one of those.\""
show a laugh
a "Ha! It's good to know this whole getup was a good idea."
show a angry
a "Did you know I actually fucking hate sports?"
"I'd never seen Allie look so angry."
show a laugh
a "Let's see... they got all the fake information I gave. That sounds about right."
#show a happy
#a: ...They don't seem to have anything on my real family, thank god."
show a surprised
a "...!"
show a worried
m "What is it?"
show a worried
a "They've been taking extremely detailed notes on me, for every single run of the show... for the past two years."
show a surprised
a "What I've done, things I've said, in private or with others... they've got notes down here to how and when I brush my teeth."
show a worried
a "How I react differently when they change the show, or tell me different things, or..."
show a sad
a "...We're nothing but lab rats to them."
show s sad
s "...It's true. And it's my fault."
show a surprised
a "What do you mean?"
show s worried
s "If I hadn't invented this machine in the first place, none of this would have ever happened."

menu:
    "You couldn't have known":
        jump p3d2_addChoice5
    "It's not your fault":
        jump p3d2_addChoice6

label p3d2_addChoice5:
m "You couldn't have known, Scarlett."
m "You really wanted to help people, and that was a great thing."
m "It's not your fault someone ended up using your invention like this."
jump p3d2_addAfter3

label p3d2_addChoice6:
m "It's not your fault, Scarlett."
m "You really wanted to help people, and that was a great thing."
m "Whoever used your invention for this is to blame, not you."

label p3d2_addAfter3:

show s sad
s "...If you say so, [name]."
show s neutral
s "My file says that they wiped my memories, and put me on the show, to make sure that..."
show s worried
s "...No one else could build this technology?"
show s sad
s "I can't even begin to understand how that could even be a reason for causing all of this."
m "...I'm not sure we'd even want to understand the people who run this place."
"The room fell silent."
"I wanted to crack a joke to ease the tension, but it was impossible."
#I looked at the folder in my own hand - I both wanted to open it to find any hints of who I used to be, as well as hide it away forever."
show s sad at pos10s with dissolve
show y happy at pos30y with dissolve
y "...Thank goodness."
"...Wait, what?"
m "I'm... surprised to hear you say that, Yui, all things considered."
show y surprised
y "Ah! Sorry, I - I know how that must have sounded. Sorry, guys."
show y sad
y "Mine's the same as Allie's. They've tracked, measured, and commented on so many things about us, so many times... it feels so violating."
show y happy
y "...But it's not all bad."
show y neutral
y "Did I ever tell you why I came on this show, [name]?"
m "No, why's that?"
show y surprised
y "Ah, I forgot. I wasn't allowed to tell you, that was one of the conditions."
m "...?"
show y happy
y "To be completely transparent... the reason was money, and nothing but that."
show y worried
y "...It was hard enough to take care of myself without my parents, let alone two sisters and a whole farm."
show y neutral
y "I remember now... it was Kat who reached out to me."
show y happy
y "She told me that if I came onto the show... my sisters and the farm would be taken care of for life."
show y laugh
y "I know this is all horrifying, but... I'm just glad she held up her end of the deal."
show y worried
y "...Even if I might die here, and never see them again..."
m "...That's too depressing to think, Yui."
m "We're going to get out of here, and see your sisters again. Together."
show y blush
y "...Thanks, [name]. I'd like that."
show t happy
t "I'm curious, Yui! What kind of archetype did they list you as?"
show y surprised
y "Uh... the \"girl next door\"."
show t angry
t "Damn it, that sounds a lot better than \"gaming gremlin\"."
show y laugh
show s laugh
show a laugh
"Yui, Scarlett, Allie, and I laughed."
"At times like these, having a mood lifter like Terra around was critical."
stop music
play music Twisting
"I looked at Violet, who had been silent the whole time."
"She was trembling."
m "...Violet, are you..."
show v sad at pos50v with easeinleft
show a worried at pos70a with easeinright
show y worried
show s worried
show t worried
v "Please, god, don't let this be true."
"Violet fell to her knees and sobbed, with her hands against her face."
"I rushed to her side."
m "Are you okay, Violet?"
"Violet threw her folder against the floor, causing the papers inside to scatter through the room."
show v angry
v "Nothing is okay, [name]!"
show v sad
v "...It doesn't matter if I get off this island or not."
show v angry
v "I'm not like you! I have nothing to go back to, no one to stand by me!"
show v worried
v "...There's nothing for me either way."
show s worried
show y worried
hide v with dissolve
m "Violet! Wait -"
"She ran to the stairs. Her sobs echoed through the staircase and through the lab."
show y worried at pos50y with easeinleft
"I moved to follow her, but Yui stopped me."

y "...I think she might need some time alone, [name]."
show y sad
y "I didn't mean to look, but after her papers scattered around the room, I..."
show y worried
y "...Maybe you should just read it."
"Yui handed me the papers that were in Violet's file."
"Violet Valentine is a special case."
"Otherwise unremarkable, but she's the daughter of Edgar and Vayne Valentine, one of our primary investors."
"It is my understanding that they have recently had a son, and want for him to be the next heir, after Viola passed."
"Edgar and Vayne sent their remaining daughter, Violet, to us, full well knowing what the show would entail."
"They've paid us an incredible sum to make sure that she does not return from the show."
"Even for me, this is too cruel."
"...It was too hard to keep reading."
"I closed the folder, and just stared open-mouthed at Yui."
"There were no words."
hide a with dissolve
hide s with dissolve
hide t with dissolve
hide y with dissolve
show bg Black with dissolve
$ renpy.pause(delay = 1.0, hard = True)
stop music
play music Bittersweet
"I knocked on Violet's door later that night, but it was no use."
#show v sad
v "Please... just leave me alone, [name]."

menu:
    "I'm here if you want to talk":
        jump p3d2_addChoice7
    "I'm sorry, Violet":
        jump p3d2_addChoice8

label p3d2_addChoice7:
m "...I'm here for you if you want to talk, Violet."
jump p3d2_addAfter4

label p3d2_addChoice8:
m "...I'm so sorry, Violet."

label p3d2_addAfter4:

#hide v
"...Even if she opened the door, I would have no idea what I was supposed to say."
"What could anyone say?"
"...I was wrong."
"It's not only up from here."
"There's a long way down..."
stop sound
show bg Black
hide a
hide s
hide t
hide v
hide y
hide k
hide d
hide b1
hide b2
hide b3
hide b4
hide b5
with dissolve
window hide dissolve
$ renpy.pause(delay = 2.0, hard = True)

jump P3Day3
